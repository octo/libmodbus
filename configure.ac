#                                               -*- Autoconf -*-
# Process this file with autoconf to produce a configure script.

AC_PREREQ(2.59)
AC_INIT(libmodbus, 2.1.0, stephane.raimbault@gmail.com)
AC_CONFIG_SRCDIR([src/modbus.c])
AC_CONFIG_HEADERS([config.h])
AM_INIT_AUTOMAKE
AM_DISABLE_STATIC

# API version
LIBMODBUS_MAJOR=`echo $PACKAGE_VERSION | cut -d'.' -f1`
LIBMODBUS_MINOR=`echo $PACKAGE_VERSION | cut -d'.' -f2`
LIBMODBUS_PATCH=`echo $PACKAGE_VERSION | cut -d'.' -f3`
AC_SUBST(LIBMODBUS_MAJOR)
AC_SUBST(LIBMODBUS_MINOR)
AC_SUBST(LIBMODBUS_PATCH)

# ABI version
LIBMODBUS_CURRENT=3
LIBMODBUS_REVISION=0
LIBMODBUS_AGE=0
AC_SUBST(LIBMODBUS_CURRENT)
AC_SUBST(LIBMODBUS_REVISION)
AC_SUBST(LIBMODBUS_AGE)

# Checks for programs.
AC_PROG_CC
AC_PROG_CXX
AC_PROG_MAKE_SET
AC_PROG_LIBTOOL

# Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS([termios.h sys/time.h unistd.h errno.h limits.h fcntl.h sys/types.h sys/socket.h sys/ioctl.h netinet/in.h netinet/ip.h netinet/tcp.h arpa/inet.h])

# Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_SIZE_T
AC_HEADER_TIME
# AC_TYPE_UINT*_T: not supported by autoconf-2.59 of CentOS 5.3
# AC_TYPE_UINT16_T
# AC_TYPE_UINT32_T
# AC_TYPE_UINT8_T

# Cygwin defines IPTOS_LOWDELAY but can't handle that flag so it's necessary to
# workaround that problem and Cygwin doesn't define MSG_DONTWAIT.
AC_CHECK_DECLS([__CYGWIN__])

# Checks for library functions.
AC_FUNC_FORK
AC_FUNC_SELECT_ARGTYPES
AC_TYPE_SIGNAL
AC_CHECK_FUNCS([gettimeofday inet_ntoa memset select socket strerror])

AC_OUTPUT([
        Makefile
        src/Makefile
        src/version.h
        tests/Makefile
        modbus.pc
])
